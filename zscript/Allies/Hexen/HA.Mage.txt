// Mage AI
class HA_Mage : HA_CoreAI
{
	enum UnlockedWep
	{
		Wand = 1,
		Frost,
		Lightning,
		Blood
	}

	override void SetActiveWeapon()
	{
		switch (WeaponCapability)
		{
			// Default (1)
			ActiveWeapon = "Wand";
			// He can always shoot
			CanShootProj = true;

			case Frost: 	ActiveWeapon = "Frost"; 	break;
			case Lightning: ActiveWeapon = "Lightning"; break;
			case Blood: 	ActiveWeapon = "Blood"; 	break;
		}
	}

	static const string WeaponList[] =
	{
		"Wand",
		"Frost",
		"Lightning",
		"Blood"
	};

	override void HA_PostBeginPlay()
	{
		ActiveWeapon = "Wand";
		IsMelee = false;
		IsRanged = true;
		Pronoun = "He";
		//Mana = 999;
		//WeaponCapability = 3;
	}

	override void HA_Tick()
	{
		if (ActiveWeapon ~== "Lightning")
		{
			if (random[LightningReady]() > 250)
			{
				if (IsActorPlayingSound(CHAN_WEAPON)) return;
				A_StartSound ("MageLightningReady", CHAN_WEAPON);
			}
		}

		//if (level.time % 35 == 0)
		//{
		//	Console.Printf("Mage Mana: %d", Mana);
		//}
	}

	// Using bools making sure it was a success shoot
	bool MAGE_Wand()
	{
		if (random[chanceshoot]() < 75)
		{
			Actor proj = A_SpawnProjectile("MageWandMissile", 55);
			if (proj)
			{
				proj.bMTHRUSPECIES = true;
				//vel.xy = (0,0);
				SetStateLabel("Missile");
				return true;
			}
		}

		return false;
	}

	bool MAGE_Frost()
	{
		if (random[chanceshoot]() > 175)
		{
			bool success = false;

			bool conedone=false;
			FTranslatedLineTarget t;
			A_StartSound ("MageShardsFire", CHAN_BODY);
			int damage = random[MageCone](90, 105);
			for (int i = 0; i < 16; i++)
			{
				double ang = angle + i*(45./16);
				double slope = AimLineAttack (ang, DEFMELEERANGE, t, 0., ALF_CHECK3D);
				if (t.linetarget)
				{
					t.linetarget.DamageMobj (self, self, damage, 'Ice', DMG_USEANGLE, t.angleFromSource);
					conedone = true;
					//vel.xy = (0,0);
					SetStateLabel("Missile");
					success = true;
					break;
				}
			}
			// didn't find any creatures, so fire projectiles
			if (!conedone)
			{
				Actor mo = SpawnPlayerMissile ("FrostMissile");
				if (mo)
				{
					mo.special1 = FrostMissile.SHARDSPAWN_LEFT|FrostMissile.SHARDSPAWN_DOWN|FrostMissile.SHARDSPAWN_UP|FrostMissile.SHARDSPAWN_RIGHT;
					mo.special2 = 3; // Set sperm count (levels of reproductivity)
					mo.target = self;
					mo.args[0] = 3;		// Mark Initial shard as super damage
				}
				//vel.xy = (0,0);
				SetStateLabel("Missile");
				success = true;
			}

			return success;
		}

		return false;
	}

	bool MAGE_Lightning()
	{
		if (random[chanceshoot]() > 225)
		{
			class<Actor> floor = "LightningFloor";
			class<Actor> ceiling = "LightningCeiling";
			LightningFloor fmo = LightningFloor(SpawnPlayerMissile (floor));
			LightningCeiling cmo = LightningCeiling(SpawnPlayerMissile (ceiling));
			if (fmo)
			{
				fmo.special1 = 0;
				fmo.lastenemy = cmo;
				fmo.A_LightningZap();
			}
			if (cmo)
			{
				cmo.tracer = NULL;
				cmo.lastenemy = fmo;
				cmo.A_LightningZap();
			}
			A_StartSound ("MageLightningFire", CHAN_BODY);
			//vel.xy = (0,0);
			SetStateLabel("Missile");
			return true;
		}

		return false;
	}

	private void Blood_MStaffSpawn (double angle, Actor alttarget)
	{
		FTranslatedLineTarget t;

		Actor mo = SpawnPlayerMissile ("MageStaffFX2", angle, pLineTarget:t);
		if (mo)
		{
			mo.target = self;
			if (t.linetarget && !t.unlinked)
				mo.tracer = t.linetarget;
			else
				mo.tracer = alttarget;
		}
	}

	bool MAGE_Blood()
	{
		if (random[chanceshoot]() > 225)
		{
			FTranslatedLineTarget t;
			// [RH] Let's try and actually track what the player aimed at
			AimLineAttack (angle, PLAYERMISSILERANGE, t, 32.);
			if (t.linetarget == NULL)
			{
				t.linetarget = RoughMonsterSearch(10, true, true);
			}
			Blood_MStaffSpawn (angle, t.linetarget);
			Blood_MStaffSpawn (angle-5, t.linetarget);
			Blood_MStaffSpawn (angle+5, t.linetarget);
			A_StartSound ("MageStaffFire", CHAN_BODY);
			//vel.xy = (0,0);
			SetStateLabel("Missile");
			return true;
		}
		return false;
	}

	// Mage cannot do melee attack
	override void HA_MeleeShoot() {}

	override void HA_RangeShoot()
	{
		//int asd = random[asd](0, WeaponList.Size()-1);
		//ActiveWeapon = WeaponList[asd];

		angle = AngleTo(target, true);
		if (!CheckLOF(0, 0, 0, 0, 0, 0, 0, AAPTR_TARGET, 0)) return;
		if (InStateSequence(CurState, ResolveState("Missile"))) return;

		// ================================= Wand
		if (ActiveWeapon ~== "Wand")
		{
			if (MAGE_Wand())
			{
				//Mana += 1;
			}
		}

		// ================================= Frost Cone 
		if (ActiveWeapon ~== "Frost")
		{
			if (MAGE_Frost())
			{
				//Mana -= 3;
			}
		}

		// ================================= Lightning Arc of Death
		if (ActiveWeapon ~== "Lightning")
		{
			if (MAGE_Lightning())
			{
				//Mana -= 5;
			}
		}

		// ================================= Bloodscourge
		if (ActiveWeapon ~== "Blood")
		{
			if (MAGE_Blood())
			{
				//Mana -= 15;
			}
		}
	}

	Default
	{
		Species "HexenAlly";
		PainSound "PlayerMagePain";
		DeathSound "PlayerMageNormalDeath";
	}
	States
	{
	Spawn:
		MAGE AAA 4;
		Loop;
	See:
		MAGE A 0 A_AlertMonsters(184, AMF_TARGETEMITTER);
		MAGE ABCD 4 A_FollowerChase();
		Loop;
	Missile:
		Goto Melee;
	Melee:
		MAGE E 0 A_AlertMonsters(512, AMF_TARGETEMITTER);
		MAGE E 4 A_AllyDodge();
		MAGE F 17 A_AllyDodge(); //was 8
		MAGE E 2 A_AllyDodge();
		Goto See;
	Pain:
		MAGE G 0 A_AlertMonsters(512, AMF_TARGETEMITTER);
		MAGE G 0 A_AllyDodge();
		MAGE G 4;
		MAGE G 4 A_Pain;
		Goto See;
	Death:
		MAGE H 6;
		MAGE I 6 A_Scream;
		MAGE JK 6;
		MAGE L 6 A_NoBlocking;
		MAGE M 6;
		MAGE N -1;
		Stop;		
	XDeath:
		MAGE O 5 A_Scream;
		MAGE P 5;
		MAGE R 5 A_NoBlocking;
		MAGE STUVW 5;
		MAGE X -1;
		Stop;
	Ice:
		MAGE Y 5 A_FreezeDeath;
		MAGE Y 1 A_FreezeDeathChunks;
		Wait;
	Burn:
		FDTH E 5 BRIGHT A_StartSound("*burndeath");
		FDTH F 4 BRIGHT;
		FDTH G 5 BRIGHT;
		FDTH H 4 BRIGHT A_Scream;
		FDTH I 5 BRIGHT;
		FDTH J 4 BRIGHT;
		FDTH K 5 BRIGHT;
		FDTH L 4 BRIGHT;
		FDTH M 5 BRIGHT;
		FDTH N 4 BRIGHT;
		FDTH O 5 BRIGHT;
		FDTH P 4 BRIGHT;
		FDTH Q 5 BRIGHT;
		FDTH R 4 BRIGHT;
		FDTH S 5 BRIGHT A_NoBlocking;
		FDTH T 4 BRIGHT;
		FDTH U 5 BRIGHT;
		FDTH V 4 BRIGHT;
		ACLO E 35 A_CheckPlayerDone;
		Wait;
		ACLO E 8;
		Stop;	
	
	}
}
