// todo: Maybe we could squash these Inventory items as one? Atleast the most important ones

// Stores information about the Player's allies, then spawns them into the level accordingly
class HA_AIHub : Inventory
{
	int TotalNum;
	Array<int> AllyHealth;
	Array<string> AllyType, AllyColor, AllyWeapon, AllyColorChat;
	Array<Actor> Allies;

	int UnlockWeaponry;

	// compatibility with other mods
	bool WoCMod;

	override void Tick()
	{
		Super.Tick();
		actor MyPlayer = owner;
		if (MyPlayer)
		{
			if (level.time % (35 * 2) == 0)
			{
				let wepP = MyPlayer.player.ReadyWeapon;
				if (wepP)
				{
					class<weapon> weapon = MyPlayer.player.ReadyWeapon.GetClassName();

					bool _; int slot;
					[_, slot] = MyPlayer.player.weapons.locateWeapon(weapon);

					if (slot > UnlockWeaponry)
					{
						int differ = slot - UnlockWeaponry;

						// Player might just never equip the previous slot weapon, so big brain time
						UnlockWeaponry += slot - UnlockWeaponry;

						if (differ == 1)
							PrintTip("Slots", slot);
						else 
							PrintTip("Slots", slot, differ);

						UnlockAllyWeaponry();
					}
				}
			}
		}
	}

	// Checks UnlockWeaponry variable and unlocks weapon for current allies on the level
	void UnlockAllyWeaponry()
	{
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i])
			{
				let Ally = HA_CoreAI(Allies[i]);
				if (Ally)
				{
					Ally.WeaponCapability = UnlockWeaponry;
				}
			}
		}
	}

	void PrintTip(string Tip, int param1 = 0, double param2 = -1)
	{
		actor MyPlayer = owner;
		if (Tip ~== "Slots")
		{
			if (MyPlayer)
			{
				if (MyPlayer.checkLocalView())
				{
					string Display = "n/a";
					string number = "n/a";

					if (param2 < 0)
					{
						switch (param1)
						{
							case 1: number = "First"; break;
							case 2: number = "Second"; break;
							case 3: number = "Third"; break;
						}

						if (param1 != 4)
							Display = string.Format("\c*Your allies are now able to use their %s weapon!", number);
						else
							Display = string.Format("\c*Your allies are now able to use all weapons!");

						Console.MidPrint(NULL, Display);
					}
					else
					{
						// For some reason... the player skipped a weapon slot, we don't know why, and we'll never know why

						// Slot 3, but the difference is 2 (Player equipped Slot 1 -> Slot 3)
						if (param1 == 3 && param2 == 2)
						{
							Display = string.Format("\c*Your allies are now able to use their Second and Third weapons!");
						}

						// We don't care, it's Slot 4
						if (param1 == 4)
						{
							Display = string.Format("\c*Your allies are now able to use all weapons!");
						}

						Console.MidPrint(NULL, Display);
					}
				}
			}
		}
	}

	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
    {
        if (owner && source is "HA_CoreAI")
		{
			newdamage = 0;
		}
    }

	override void PostBeginPlay()
	{
		TotalNum = 0;
		UnlockWeaponry = 1;
	}

	// Add an Ally for restoration
	// Preserves everything, with the exception of Actor array, which needs to be refilled
	// after a new level
	void AddAlly(string Type, string Color, string Weapon, int Health, actor Ally, string ChatColor)
	{
		TotalNum++;
		//Console.Printf("TotalNum: %d", TotalNum);
		let AA = HA_CoreAI(Ally);
		if (AA)
		{
			AA.SpawnSlot = TotalNum;
		}

		AllyHealth.Push(Health);
		AllyType.Push(Type);
		AllyColor.Push(Color);
		AllyWeapon.Push(Weapon);
		Allies.Push(Ally);
		AllyColorChat.Push(ChatColor);
	}

	// Update information on the allies before destroying them, and changing onto the next level
	// i.e. They should not have more Health if they were hurt previously
	// Look into HA.EventHandler.txt, WorldUnloaded func
	void UpdateAlly()
	{
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i])
			{
				let Ally = HA_CoreAI(Allies[i]);
				if (Ally)
				{
					AllyHealth[i] = Ally.Health;
					AllyWeapon[i] = Ally.ActiveWeapon;
				}
			}
		}
	}

	// Remove an Ally from restoration
	// Mainly because they died
	// Look also in HA.EventHandler how actors are getting removed on each level reset
	void RemoveAlly(actor Whom)
	{	
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i] && Whom && Allies[i] == Whom)
			{
				AllyHealth.Delete(i);
				AllyType.Delete(i);
				AllyColor.Delete(i);
				AllyWeapon.Delete(i);
				Allies.Delete(i);
				AllyColorChat.Delete(i);
				TotalNum--;
			}
		}
	}

	void RestoreAlly()
	{
		// Clear this array because the pointers will become garbage after a level change
		Allies.Clear();

		for (uint i = 0; i < AllyType.Size(); ++i)
		{
			class<Actor> AllySpawn = null;

			if (AllyType[i] ~== "Fighter") AllySpawn = "HA_Fighter";
			if (AllyType[i] ~== "Cleric") AllySpawn = "HA_Cleric";
			if (AllyType[i] ~== "Mage") AllySpawn = "HA_Mage";

			actor Ally = spawn(AllySpawn, owner.pos, ALLOW_REPLACE);
			if (Ally)
			{
				let HAlly = HA_CoreAI(Ally);
				if (HAlly)
				{
					if (AllyColor[i] != "default")
						HAlly.A_SetTranslation(AllyColor[i]);
					
					//HAlly.Health = AllyHealth[i];
					HAlly.A_SetHealth(AllyHealth[i]);
					HAlly.MyPlayer = owner;
					HAlly.HexenType = AllyType[i];
					HAlly.gotType = true;
					HAlly.Restored = true;
					HAlly.HexenColor = AllyColorChat[i];
					HAlly.WeaponCapability = UnlockWeaponry;
					HAlly.SetStateLabel("See");

					// Set the name properly for the Ally
					HAlly.SetTag(HAlly.AllyName());

					if (WoCMod)
					{
						// No experience for now, will have to save that too into an array
						HAlly.ACS_NamedExecuteAlways("WOC Monster Init", 0, 256, 0);
						HAlly.WoC = true;
						
						string WoCItem1 = "CurrentMaxHealth";
						string WoCItem2 = "Level";
						class<Inventory> CurrentMaxHealth = WoCItem1;
						class<Inventory> Level = WoCItem2;
						
						// Fix bug with the HUD
						HAlly.A_SetInventory(CurrentMaxHealth, 100);
						HAlly.A_SetInventory(Level, 1);
					}

					// Push the new pointer to the array
					Allies.Push(HAlly);
				}
			}
		}
	}

	// If one Ally has been spawned already, check for the other class that's not a duplicate
	// and isn't the Player's doppelganger
	int NonDuplicateClass(int ActiveClass)
	{
		Array<int> NonDuplicateClasses;

		if (TotalNum != 0)
		{
			for (uint i = 0; i < Allies.size(); i++)
			{
				if (Allies[i])
				{
					let Ally = HA_CoreAI(Allies[i]);
					if (Ally)
					{
						for (uint j = 0; j < HA_AllySpawner.HexenAllyList.Size(); j++)
						{
							if (!(Ally.HexenType ~== HA_AllySpawner.HexenAllyList[j]))
							{
								if (j != ActiveClass)
								{
									NonDuplicateClasses.Push(j);
								}
							}
						}
					}
				}
			}
		}
		else
		{
			Console.Printf("Hexen Allies: Warning, tried to use NonDuplicateClass without a single Ally spawned (TotalNum variable)");
			Console.Printf("Hexen Allies: This causes an array out of bounds.");
		}

		int rng = random[cls](0, NonDuplicateClasses.Size()-1);
		return NonDuplicateClasses[rng];
	}

	Default
	{
		+INVENTORY.UNDROPPABLE;
       	+INVENTORY.UNTOSSABLE;
       	+INVENTORY.PERSISTENTPOWER;
       	inventory.maxamount 1;
	}
}

class HA_AllySpawner : CustomInventory
{
	static const string HexenListName[] =
	{ // Uses actual PlayerClass names
		"FighterPlayer",
		"ClericPlayer",
		"MagePlayer"
	};

	static const class<actor> HexenSpawnList[] =
	{
		"HA_Fighter",
		"HA_Cleric",
		"HA_Mage"
	};

	static const string HexenColors[] =
	{
		"Green",
		"Red",
		"Blue",
		"Silver",
		"Yellow"
	};

	enum eHexenColors
	{
		HA_GREEN,
		HA_RED,
		HA_BLUE,
		HA_SILVER,
		HA_YELLOW,
	}

	static const string HexenAllyList[] =
	{
		"Fighter",
		"Cleric",
		"Mage"
	};

	private void SpawnAlly()
	{		
		if (owner)
		{
			actor MyPlayer = owner;
			
			if (!MyPlayer) return;

			Console.Printf("Souls: %d | Spawner: %d", MyPlayer.CountInv("HA_Soul"), MyPlayer.CountInv("HA_AllySpawner"));

			// Only 2 Active Ally
			// todo: Add a cvar for disabling this
			let Hub = MyPlayer.FindInventory("HA_AIHub");
			if (Hub)
			{
				let HubInv = HA_AIHub(Hub);
				if (HubInv)
				{
					if (HubInv.TotalNum >= MyPlayer.CountInv("HA_AllySlot"))
					{
						string Req = String.Format("\c[red]You reached the maximum limit for Ally spawning (%d)", MyPlayer.CountInv("HA_AllySlot"));
						if (MyPlayer.checkLocalView())
						{
							Console.MidPrint(NULL, Req);
						}
						return;
					}
				}
			}

			int CostToSpawn = 5;
			if (MyPlayer.CountInv("HA_Soul") < CostToSpawn)
			{
				int Req = CostToSpawn - MyPlayer.CountInv("HA_Soul");
				string Requ = String.Format("\c[red]Not enough souls to spawn Ally!\n\c[silver]You need %d more", Req);
				if (MyPlayer.checkLocalView())
				{
					Console.MidPrint(NULL, Requ);
				}
				MyPlayer.A_StartSound("DoorLocked", CHAN_BODY);
				return;
			}

			MyPlayer.A_TakeInventory("HA_Soul", CostToSpawn);
			int Souls = MyPlayer.CountInv("HA_Soul");
			MyPlayer.A_SetInventory("HA_AllySpawner", Souls);

			//if (MyPlayer.CountInv("HA_Soul") == 0)
				//MyPlayer.A_GiveInventory("HA_Soul", 1);

			string PlayerClass = MyPlayer.GetClassName();

			//------------ WoC support

			// Check for an item that is 100% always in the mod, and not in any other mod
			bool isWoCLoaded = false;
			string WoCItem = "WoC_Stat";
			class<Actor> woc = WoCItem;
			if (woc)
			{
				Console.Printf("\c[red]Hexen Allies: \c[yellow]Wrath of Cronos \c[red]detected!");
				isWoCLoaded = true;
				
				// Make the Hub inventory item aware of WoC
				if (Hub)
				{
					let HubInv = HA_AIHub(Hub);
					if (HubInv)
					{
						HubInv.WoCMod = true;
					}
				}
			}

			//------------------------------ WoC support
			if (isWoCLoaded)
			{
				if (PlayerClass ~== "NewFighterPlayer")
					PlayerClass = "FighterPlayer";
				if (PlayerClass ~== "NewClericPlayer")
					PlayerClass = "ClericPlayer";
				if (PlayerClass ~== "NewMagePlayer")
					PlayerClass = "MagePlayer";
				// todo: #1 WoC: Add Ally version of Hunter, Necromancer, and Assassin
				if (PlayerClass ~== "NewHunterPlayer" ||
					PlayerClass ~== "NewNecromancerPlayer" ||
					PlayerClass ~== "NewAssassinPlayer")
					{
						Console.Printf("\c[red]Hexen Allies: \c[yellow]The Playerclass you play as doesn't have an Ally version!");
						int randomclass = random[woc](0, HexenListName.Size()-1);
						PlayerClass = HexenListName[randomclass];
					}
			}
			//------------------------------

			int TotalAlliesSpawned = 0;
			int Chosen = 0;

			if (Hub)
			{
				let HubInv = HA_AIHub(Hub);
				if (HubInv)
				{
					TotalAlliesSpawned = HubInv.TotalNum;
				}
			}

			if (TotalAlliesSpawned <= 2)
			{
				int ActiveClass = 0;
				Array<int> AvailableClass;
				for (int i = 0; i < HexenListName.Size(); i++)
				{
					if (PlayerClass ~== HexenListName[i])
					{
						ActiveClass = i;
					}

					if (!(PlayerClass ~== HexenListName[i]))
					{
						AvailableClass.Push(i);
					}
				}

				// Randomize the Ally's class on spawn
				int chsn = random[choose](0, AvailableClass.Size()-1);
				Chosen = AvailableClass[chsn];

				// The idea here is that until the Player has 2 allies do not spawn an Ally class identical to the Player's
				if (Hub && TotalAlliesSpawned > 0)
				{
					let HubInv = HA_AIHub(Hub);
					if (HubInv) Chosen = HubInv.NonDuplicateClass(ActiveClass);
				}

				// We can just assume the Third Ally's class will always be the same like the Player's
				if (TotalAlliesSpawned == 2)
				{
					Chosen = ActiveClass;
				}
			}
			else
			{
				// Go amok, I guess!
				Chosen = random[tas](0, HexenSpawnList.Size());
			}

			int UnlockableWeapons = 1;
			if (Hub)
			{
				let HubInv = HA_AIHub(Hub);
				if (HubInv) UnlockableWeapons = HubInv.UnlockWeaponry;
			}

			actor SpawnAI = spawn(HexenSpawnList[Chosen], MyPlayer.pos, ALLOW_REPLACE);
			if (SpawnAI)
			{
				let SpawnedAI = HA_CoreAI(SpawnAI);
				if (SpawnedAI)
				{
					SpawnedAI.MyPlayer = MyPlayer;
					SpawnedAI.HexenType = HexenAllyList[Chosen];
					SpawnedAI.gotType = true;
					SpawnedAI.WeaponCapability = UnlockableWeapons;
					SpawnedAI.SetStateLabel("See");
					// Say hooray for Color randomization!
					int rndColor = random[clr](0, HexenColors.Size()-1);
					string AIcolor = "n/a";
					if (SpawnedAI.HexenType ~== "Cleric" || 
						SpawnedAI.HexenType ~== "Mage")
					{
						AIcolor = String.Format("CM%s", HexenColors[rndColor]);
						// Cleric and Mage is blue by default, so we don't need to set it
						if (rndColor != HA_BLUE)
							SpawnedAI.A_SetTranslation(AIcolor);
						else AIcolor = "default";
					}
					else
					{
						AIcolor = String.Format("F%s", HexenColors[rndColor]);
						// Fighter is Silver by default
						if (rndColor != HA_SILVER)
							SpawnedAI.A_SetTranslation(AIcolor);
						else AIcolor = "default";
					}
					// Needed for level restoration, this uses actual translation value
					SpawnedAI.HexenColorTrnsl = AIcolor;

					// Needed for chat coloring
					SpawnedAI.HexenColor = HexenColors[rndColor];

					// Execute function from Player's inventory
					let HubStuff = MyPlayer.FindInventory("HA_AIHub");
					if (HubStuff)
					{
						let Inven = HA_AIHub(HubStuff);
						if (Inven)
						{
							// Store Ally information
							Inven.AddAlly(SpawnedAI.HexenType, AIcolor, "null", SpawnedAI.Health, SpawnedAI, SpawnedAI.HexenColor);
						}
					}

					SpawnedAI.AllySay("Ready for action!");
					SpawnedAI.Following = true;

					SpawnedAI.SetTag(SpawnedAI.AllyName());

					// Settings for AI after WoC is detected
					if (isWoCLoaded)
					{
						// No experience for now, will have to save that too into an array
						SpawnedAI.ACS_NamedExecuteAlways("WOC Monster Init", 0, 256, 0);
						SpawnedAI.WoC = true;
						
						string WoCItem1 = "CurrentMaxHealth";
						string WoCItem2 = "Level";
						class<Inventory> CurrentMaxHealth = WoCItem1;
						class<Inventory> Level = WoCItem2;
						
						// Force set this for now
						SpawnedAI.A_SetInventory(CurrentMaxHealth, 100);
						SpawnedAI.A_SetInventory(Level, 1);
					}

				}
			}
		}
	}

	Default
	{
		+COUNTITEM
		+FLOATBOB
		+INVENTORY.INVBAR
		+INVENTORY.UNDROPPABLE;
       	+INVENTORY.UNTOSSABLE;
       	+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.KEEPDEPLETED;
		Inventory.PickupFlash "PickupFlash";
		+INVENTORY.FANCYPICKUPSOUND
		Inventory.Icon "HASPAWN";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "Picked up an Ally Spawner. Wait... that's illegal";
		Inventory.Amount 1;
		Inventory.MaxAmount 999;
		Tag "Ally Spawner";
	}
	States
	{
	Spawn:
		EGGC ABCB 6;
		Loop;
	Use:
		TNT1 A 0 { invoker.SpawnAlly(); }
		Fail;
	}
}

class HA_AllySlot : CustomInventory
{
	private void TryIncrementing()
	{		
		if (owner)
		{
			actor MyPlayer = owner;
			int Cost = 21;

			if (MyPlayer.CountInv("HA_Soul") < Cost)
			{
				int Req = Cost - MyPlayer.CountInv("HA_Soul");
				string Requ = String.Format("\c[red]Not enough souls to add a slot!\n\c[silver]You need %d more", Req);
				if (MyPlayer.checkLocalView())
				{
					Console.MidPrint(NULL, Requ);
				}
				MyPlayer.A_StartSound("DoorLocked", CHAN_BODY);
				return;
			}
		}
	}

	Default
	{
		+COUNTITEM
		+FLOATBOB
		+INVENTORY.INVBAR
		+INVENTORY.UNDROPPABLE;
       	+INVENTORY.UNTOSSABLE;
       	+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.KEEPDEPLETED;
		Inventory.PickupFlash "PickupFlash";
		+INVENTORY.FANCYPICKUPSOUND
		Inventory.Icon "HAINCRE";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "Picked up an Ally Slot Incrementor. Who's going to jail for this?";
		Inventory.Amount 1;
		Inventory.MaxAmount 9;
		Tag "Total Ally Slots";
	}
	States
	{
	Spawn:
		EGGC ABCB 6;
		Loop;
	Use:
		TNT1 A 0 { invoker.TryIncrementing(); }
		Fail;
	}
}

class HA_Soul : Ammo
{
	void SoulToAmount(bool FirstPickup = false)
	{
		int Souls = owner.CountInv("HA_Soul");
		if (!FirstPickup) Souls += 1;
		
		owner.A_SetInventory("HA_AllySpawner", Souls);
	}

	override bool HandlePickup (Inventory item)
	{
		if (item.GetClass() == "HA_Soul")
		{
			if (owner)
			{
				SoultoAmount();
			}
		}
		return Super.HandlePickup (item);
	}

	override void AttachToOwner (actor other)
	{
		Super.AttachToOwner(other);
		if (owner)
		{
			SoulToAmount(true);
		}
	}

	Default
	{
		Inventory.PickupMessage "Picked up a soul.";
		Inventory.Amount 1;
		Inventory.MaxAmount 999;
		Ammo.BackpackAmount 1;
		Ammo.BackpackMaxAmount 999;
		Inventory.Icon "EMOMA0";
		Tag "Renewable Soul";
		Scale 1.65;
		RenderStyle "Translucent";
		Alpha 0.65;
		+FLOATBOB
		+NOGRAVITY
	}
	States
	{
	Spawn:
		XQXQ A -1;
		Stop;
	}
}