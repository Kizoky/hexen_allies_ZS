// Prevents Friendly Fire
// Cross-mod compatible
class HA_FriendlyFire : Inventory
{
    Default
	{
       	+INVENTORY.UNDROPPABLE;
       	+INVENTORY.UNTOSSABLE;
       	+INVENTORY.PERSISTENTPOWER;
       	inventory.maxamount 1;
    }

    override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
    {
        if (owner && source is "HA_CoreAI")
		{
			newdamage = 0;
		}
    }
}

// Stores information about the Player's allies, then spawns them into the level accordingly
class HA_AIHub : Inventory
{
	int TotalNum;
	Array<int> AllyHealth;
	Array<string> AllyType, AllyColor, AllyWeapon, AllyColorChat;
	Array<Actor> Allies;

	bool WoCMod;

	override void PostBeginPlay()
	{
		TotalNum = 0;
	}

	// Add an Ally for restoration
	// Preserves everything, with the exception of Actor array, which needs to be refilled
	// after a new level
	void AddAlly(string Type, string Color, string Weapon, int Health, actor Ally, string ChatColor)
	{
		TotalNum++;
		let AA = HA_CoreAI(Ally);
		if (AA)
		{
			AA.SpawnSlot = TotalNum;
		}

		AllyHealth.Push(Health);
		AllyType.Push(Type);
		AllyColor.Push(Color);
		AllyWeapon.Push(Weapon);
		Allies.Push(Ally);
		AllyColorChat.Push(ChatColor);
	}

	// Update information on the allies before destroying them, and changing onto the next level
	// i.e. They should not have more Health if they were hurt previously
	// Look into HA.EventHandler.txt, WorldUnloaded func
	void UpdateAlly()
	{
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i])
			{
				let Ally = HA_CoreAI(Allies[i]);
				if (Ally)
				{
					AllyHealth[i] = Ally.Health;
					AllyWeapon[i] = Ally.ActiveWeapon;
				}
			}
		}
	}

	// Remove an Ally from restoration
	// Mainly because they died
	// Look also in HA.EventHandler how actors are getting removed on each level reset
	void RemoveAlly(actor Whom)
	{	
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i] && Whom && Allies[i] == Whom)
			{
				AllyHealth.Delete(i);
				AllyType.Delete(i);
				AllyColor.Delete(i);
				AllyWeapon.Delete(i);
				Allies.Delete(i);
				AllyColorChat.Delete(i);
				TotalNum--;
			}
		}
	}

	void RestoreAlly()
	{
		// Clear this array because the pointers will become garbage after a level change
		Allies.Clear();

		for (uint i = 0; i < AllyType.Size(); ++i)
		{
			class<Actor> AllySpawn = null;

			if (AllyType[i] ~== "Fighter") AllySpawn = "HA_Fighter";
			if (AllyType[i] ~== "Cleric") AllySpawn = "HA_Cleric";
			if (AllyType[i] ~== "Mage") AllySpawn = "HA_Mage";

			actor Ally = spawn(AllySpawn, owner.pos, ALLOW_REPLACE);
			if (Ally)
			{
				let HAlly = HA_CoreAI(Ally);
				if (HAlly)
				{
					if (AllyColor[i] != "default")
						HAlly.A_SetTranslation(AllyColor[i]);
					
					//HAlly.Health = AllyHealth[i];
					HAlly.A_SetHealth(AllyHealth[i]);
					HAlly.MyPlayer = owner;
					HAlly.HexenType = AllyType[i];
					HAlly.gotType = true;
					HAlly.Restored = true;
					HAlly.HexenColor = AllyColorChat[i];
					HAlly.SetStateLabel("See");

					// Set the name properly for the Ally
					HAlly.SetTag(HAlly.AllyName());

					if (WoCMod)
					{
						// No experience for now, will have to save that too into an array
						HAlly.ACS_NamedExecuteAlways("WOC Monster Init", 0, 256, 0);
						HAlly.WoC = true;

						// Fix bug with the HUD
						HAlly.A_SetInventory("CurrentMaxHealth", 100);
						HAlly.A_SetInventory("Level", 1);
					}

					// Push the new pointer to the array
					Allies.Push(HAlly);
				}
			}
		}
	}

	// If one Ally has been spawned already, check for the other class that's not a duplicate
	// and isn't the Player's doppelganger
	int NonDuplicateClass(int ActiveClass)
	{
		int NewClass = 0;

		if (TotalNum != 0)
		{
			for (uint i = 0; i < Allies.size(); i++)
			{
				if (Allies[i])
				{
					let Ally = HA_CoreAI(Allies[i]);
					if (Ally)
					{
						for (uint j = 0; j < HA_AllySpawner.HexenAllyList.Size(); j++)
						{
							if (!(Ally.HexenType ~== HA_AllySpawner.HexenAllyList[j]))
							{
								if (j != ActiveClass)
								{
									NewClass = j;
									break;
								}
							}
						}
					}
				}
			}
		}

		return NewClass;
	}

	Default
	{
		+INVENTORY.UNDROPPABLE;
       	+INVENTORY.UNTOSSABLE;
       	+INVENTORY.PERSISTENTPOWER;
       	inventory.maxamount 1;
	}
}

class HA_AllySpawner : CustomInventory
{
	static const string HexenListName[] =
	{ // Uses actual PlayerClass names
		"FighterPlayer",
		"ClericPlayer",
		"MagePlayer"
	};

	static const class<actor> HexenSpawnList[] =
	{
		"HA_Fighter",
		"HA_Cleric",
		"HA_Mage"
	};

	static const string HexenColors[] =
	{
		"Green",
		"Red",
		"Blue",
		"Silver",
		"Yellow"
	};

	static const string HexenAllyList[] =
	{
		"Fighter",
		"Cleric",
		"Mage"
	};

	private void SpawnAlly()
	{		
		if (owner)
		{
			actor MyPlayer = owner;
			
			// Only 2 Active Ally
			// todo: Add a cvar for disabling this
			let Hub = MyPlayer.FindInventory("HA_AIHub");
			let HubInv = HA_AIHub(Hub);
			if (Hub && HubInv)
			{
				if (HubInv.TotalNum >= 2)
				{
					if (MyPlayer.checkLocalView())
					{
						Console.MidPrint(NULL, "\c[red]You reached the maximum limit for Ally spawning (2)");
						return;
					}
				}
			}

			if (MyPlayer.CountInv("HA_AllySpawner") < 5)
			{
				if (MyPlayer.checkLocalView())
				{
					int Req = 5 - MyPlayer.CountInv("HA_AllySpawner");
					string Requ = String.Format("\c[red]Not enough souls to spawn Ally!\n\c[silver]You need %d more", Req);
					Console.MidPrint(NULL, Requ);
				}
				owner.A_StartSound("DoorLocked", CHAN_BODY);
				return;
			}

			MyPlayer.A_TakeInventory("HA_AllySpawner", 5);

			if (MyPlayer.CountInv("HA_AllySpawner") == 0)
				MyPlayer.A_GiveInventory("HA_AllySpawner", 1);

			string PlayerClass = MyPlayer.GetClassName();

			//------------ WoC support

			// Check for an item that is 100% always in the mod, and not in any other mod
			bool isWoCLoaded = false;
			string WoCItem = "WoC_Stat";
			class<Actor> woc = WoCItem;
			if (woc)
			{
				Console.Printf("\c[red]Hexen Allies: \c[yellow]Wrath of Cronos \c[red]detected!");
				isWoCLoaded = true;
				
				// Make the Hub inventory item aware of WoC
				if (Hub && HubInv)
				{
					HubInv.WoCMod = true;
				}
			}

			if (isWoCLoaded)
			{
				if (PlayerClass ~== "NewFighterPlayer")
					PlayerClass = "FighterPlayer";
				if (PlayerClass ~== "NewClericPlayer")
					PlayerClass = "ClericPlayer";
				if (PlayerClass ~== "NewMagePlayer")
					PlayerClass = "MagePlayer";
				// todo: #1 WoC: Add Ally version of Hunter, Necromancer, and Assassin
				if (PlayerClass ~== "NewHunterPlayer" ||
					PlayerClass ~== "NewNecromancerPlayer" ||
					PlayerClass ~== "NewAssassinPlayer")
					{
						Console.Printf("\c[red]Hexen Allies: \c[yellow]The Playerclass you play as doesn't have an Ally version!");
						int randomclass = random[woc](0, HexenListName.Size()-1);
						PlayerClass = HexenListName[randomclass];
					}
			}
			//------------ WoC support

			// todo: Add a cvar for ignoring Playerclass
			int ActiveClass = 0;
			int Available[2];
			int j = 0;
			for (int i = 0; i < HexenListName.Size(); i++)
			{
				if (PlayerClass ~== HexenListName[i])
				{
					ActiveClass = i;
				}

				if (!(PlayerClass ~== HexenListName[i]))
				{
					Available[j] = i;
					j++;
				}
			}

			// Randomize the Ally's class on spawn
			int Chosen = randompick[choseone](Available[0], Available[1]);

			// Check if there's an Ally already spawned (this should also be cvar dependent, ignoring playerclass)
			// If TotalNum is more than 3 then we can just mindlessly randomize the classes
			if (Hub && HubInv && HubInv.TotalNum > 0 && HubInv.TotalNum < 3)
			{
				if (HubInv.TotalNum != 0)
				{
					Chosen = HubInv.NonDuplicateClass(ActiveClass);

					// We can just assume the Third Ally will always be the Player's class
					if (HubInv.TotalNum == 2)
					{
						Chosen = ActiveClass;
					}
				}
			}

			actor SpawnAI = spawn(HexenSpawnList[Chosen], MyPlayer.pos, ALLOW_REPLACE);
			if (SpawnAI)
			{
				let SpawnedAI = HA_CoreAI(SpawnAI);
				if (SpawnedAI)
				{
					SpawnedAI.MyPlayer = MyPlayer;
					SpawnedAI.HexenType = HexenAllyList[Chosen];
					SpawnedAI.gotType = true;
					SpawnedAI.SetStateLabel("See");
					// Say hooray for Color randomization!
					int rndColor = random[clr](0, HexenColors.Size()-1);
					string AIcolor = "n/a";
					if (SpawnedAI.HexenType ~== "Cleric" || 
						SpawnedAI.HexenType ~== "Mage")
					{
						AIcolor = String.Format("CM%s", HexenColors[rndColor]);
						// Cleric and Mage is blue by default, so we don't need to set it
						if (rndColor != 2)
							SpawnedAI.A_SetTranslation(AIcolor);
						else AIcolor = "default";
					}
					else
					{
						AIcolor = String.Format("F%s", HexenColors[rndColor]);
						// Fighter is Silver by default
						if (rndColor != 3)
							SpawnedAI.A_SetTranslation(AIcolor);
						else AIcolor = "default";
					}
					// Needed for level restoration, this uses actual translation value
					SpawnedAI.HexenColorTrnsl = AIcolor;

					// Needed for chat coloring
					SpawnedAI.HexenColor = HexenColors[rndColor];

					// Execute function from Player's inventory
					let HubStuff = MyPlayer.FindInventory("HA_AIHub");
					let Inven = HA_AIHub(HubStuff);
					if (HubStuff && Inven)
					{
						// Store Ally information
						Inven.AddAlly(SpawnedAI.HexenType, AIcolor, "null", SpawnedAI.Health, SpawnedAI, SpawnedAI.HexenColor);
					}

					SpawnedAI.AllySay("Ready for action!");
					SpawnedAI.Following = true;

					SpawnedAI.SetTag(SpawnedAI.AllyName());

					// Settings for AI after WoC is detected
					if (isWoCLoaded)
					{
						// No experience for now, will have to save that too into an array
						SpawnedAI.ACS_NamedExecuteAlways("WOC Monster Init", 0, 256, 0);
						SpawnedAI.WoC = true;

						// Force set this for now
						SpawnedAI.A_SetInventory("CurrentMaxHealth", 100);
						SpawnedAI.A_SetInventory("Level", 1);
					}

				}
			}
		}
	}

	Default
	{
		+COUNTITEM
		+FLOATBOB
		+INVENTORY.INVBAR
		+INVENTORY.UNDROPPABLE;
       	+INVENTORY.UNTOSSABLE;
       	+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.KEEPDEPLETED;
		Inventory.PickupFlash "PickupFlash";
		+INVENTORY.FANCYPICKUPSOUND
		Inventory.Icon "ARTIALLY";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "Picked up an Ally Spawner. Excuse me, WHAT???";
		Inventory.Amount 1;
		Inventory.MaxAmount 999;
		Tag "Ally Spawner";
	}
	States
	{
	Spawn:
		EGGC ABCB 6;
		Loop;
	Use:
		TNT1 A 0 { invoker.SpawnAlly(); }
		Fail;
	}
}

class HA_Soul : Ammo
{
	override bool HandlePickup (Inventory item)
	{
		if (item.GetClass() == "HA_Soul")
		{
			if (owner)
			{
				owner.A_GiveInventory("HA_AllySpawner", 1);
				owner.A_TakeInventory("HA_Soul", 1);
			}
		}
		return Super.HandlePickup (item);
	}

	override void AttachToOwner (actor other)
	{
		Super.AttachToOwner(other);
		if (owner)
		{
			owner.A_GiveInventory("HA_AllySpawner", 1);
		}
	}

	Default
	{
		Inventory.PickupMessage "Picked up a soul.";
		Inventory.Amount 1;
		Inventory.MaxAmount 999;
		Ammo.BackpackAmount 1;
		Ammo.BackpackMaxAmount 999;
		Inventory.Icon "EMOMA0";
		Tag "Renewable Soul";
		Scale 1.65;
		RenderStyle "Translucent";
		Alpha 0.65;
		+FLOATBOB
		+NOGRAVITY
	}
	States
	{
	Spawn:
		XQXQ A -1;
		Stop;
	}
}