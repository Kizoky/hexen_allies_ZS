// HA_CoreAI spawns first, then according to what kind of class the Player is playing as 
// spawns the correspending AI (HA_Figher, HA_Cleric, HA_Mage)
class HA_CoreAI : Actor
{
	actor MyPlayer, attacker;
	string HexenType, HexenColorTrnsl, HexenColor;
	bool gotType, Restored;
	int SpawnSlot;
	
	int CountdownToDeath;
	
	bool IsMelee, IsRanged;
	bool CDtoTele;

	string ActiveWeapon;
	
	virtual void SearchForPlayer()
	{
		if (MyPlayer) return;
		
		BlockThingsIterator it = BlockThingsIterator.Create (self, 256);
		
		while (it.Next () && it.Thing) 
		{
			let next = it.Thing;
			
			if (next)
			{
				if (next is "PlayerPawn" && next != self && !(next is "HA_CoreAI"))
				{
					MyPlayer = next;
					
					if (MyPlayer && !gotType)
					{
						MyPlayer.A_GiveInventory("HA_FriendlyFire", 1);
						MyPlayer.A_GiveInventory("HA_AIHub", 1);
						
						string PlayerClass = MyPlayer.GetClassName();

						//------------ WoC support
						/* todo:
						BaseHunterPlayer
						BaseNecromancerPlayer
						BaseAssassinPlayer
						*/
						string WoCFighter = "BaseFighterPlayer";
						string WoCCleric = "BaseClericPlayer";
						string WoCMage = "BaseMagePlayer";

						if (PlayerClass ~== WoCFighter)
							PlayerClass = "FighterPlayer";
						if (PlayerClass ~== WoCCleric)
							PlayerClass = "ClericPlayer";
						if (PlayerClass ~== WoCMage)
							PlayerClass = "MagePlayer";
						//------------ WoC support

						int Available[2];
						int j = 0;
						for (int i = 0; i < HexenListName.Size(); i++)
						{
							if (!(PlayerClass ~== HexenListName[i]))
							{
								Available[j] = i;
								j++;
							}
						}
						
						// not efficent way of doing this but whatever
						int Chosen = randompick[choseone](Available[0], Available[1]);

						actor SpawnAI = spawn(HexenSpawnList[Chosen], self.pos, ALLOW_REPLACE);

						if (SpawnAI)
						{
							let SpawnedAI = HA_CoreAI(SpawnAI);
							if (SpawnedAI)
							{
								SpawnedAI.MyPlayer = MyPlayer;
								SpawnedAI.HexenType = HexenAllyList[Chosen];

								SpawnedAI.gotType = true;

								SpawnedAI.SetStateLabel("See");

								// Say hooray for Color randomization!
								int rndColor = random[clr](0, HexenColors.Size()-1);

								string AIcolor = "n/a";
								if (SpawnedAI.HexenType ~== "Cleric" || 
									SpawnedAI.HexenType ~== "Mage")
								{
									AIcolor = String.Format("CM%s", HexenColors[rndColor]);

									// Cleric and Mage is blue by default, so we don't need to set it
									if (rndColor != 2)
										SpawnedAI.A_SetTranslation(AIcolor);
									else AIcolor = "default";
								}
								else
								{
									AIcolor = String.Format("F%s", HexenColors[rndColor]);

									// Fighter is Silver by default
									if (rndColor != 3)
										SpawnedAI.A_SetTranslation(AIcolor);
									else AIcolor = "default";
								}

								// Needed for level restoration, this uses actual translation value
								SpawnedAI.HexenColorTrnsl = AIcolor;

								// Needed for chat coloring
								SpawnedAI.HexenColor = HexenColors[rndColor];

								// Execute function from Player's inventory
								let HubStuff = MyPlayer.FindInventory("HA_AIHub");
								let Inven = HA_AIHub(HubStuff);
								if (HubStuff && Inven && !Restored)
								{
									// Store Ally information
									Inven.AddAlly(SpawnedAI.HexenType, AIcolor, "null", SpawnedAI.Health, SpawnedAI, SpawnedAI.HexenColor);
								}

								SpawnedAI.AllySay("Ready for action!");

								// Destroy the "spawner"
								// I think there's a better way of making a proper spawner, but this should do as of now
								Destroy();
								break;
							}
						}
					}
				}
			}
		}
	}
	
	static const string HexenColors[] =
	{
		"Green",
		"Red",
		"Blue",
		"Silver",
		"Yellow"
	};

	static const class<actor> HexenSpawnList[] =
	{
		"HA_Fighter",
		"HA_Cleric",
		"HA_Mage"
	};

	static const string HexenListName[] =
	{ // Uses actual PlayerClass names
		"FighterPlayer",
		"ClericPlayer",
		"MagePlayer"
	};
	
	static const string HexenAllyList[] =
	{
		"Fighter",
		"Cleric",
		"Mage"
	};
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		SearchForPlayer();
		spawn("Teleportfog",pos);
	}
	
	virtual void A_MovementChase(actor targ)
	{
		if (targ)
		{
			/* Jumping
			// Currently buggy so it's disabled
			FLineTraceData lineData;
			LineTrace(angle,50,0, offsetz: 5, data: LineData);
			if (LineData.HitType == TRACE_HitWall)
			{
				if (pos.z ~== floorz)
				{
					Angle = AngleTo(targ);
					A_ChangeVelocity (0, 0, 7, 0);
					A_Recoil(-1.00);
				}
			}
			*/

			int oldDir = moveDir;
			moveDir = (8 * AngleTo(targ, true) / 360) % 8;
			if (!TryWalk())
			{
				moveDir = oldDir;
				if(!TryWalk())
				{
					for (int i = 1; i <= 4; ++i)
					{
						moveDir = (oldDir + i) % 8;
						if (TryWalk())
							break;

						moveDir = (oldDir - i) % 8;
						if (TryWalk())
							break;
					}
				}
			}

			// Face movement direction
			angle = 360 * moveDir / 8;
			if (angle > 180)
				angle -= 360;

		}
	}
	
	int TimeBeforeRandomTurn, TimeProbablyGotStuck;
	virtual void A_FollowerChase()
	{
		if ((target && target.bKILLED) || (target && target is "PlayerPawn"))
		{
			target = null;
			TimeProbablyGotStuck = 0;
		}
		
		// Follow our player
		if (!target && MyPlayer)
		{
			if (Distance3D(MyPlayer) > 100)
			{
				vector2 PlayerPos = (MyPlayer.pos.x,MyPlayer.pos.y);
				if (CheckSight (MyPlayer, SF_SeePastShootableLines) && CheckMove(PlayerPos, flags: PCM_NOLINES))
				{
					A_MovementChase(MyPlayer);
					Angle = AngleTo(MyPlayer);
					A_Recoil(-1.75);
				}
				else
				{
					// Note: There's a small.. or rather huge chance the AI will get stuck for some reason while
					// trying to catch up to the Player, we'll countdown to 15 and teleport it back to the Player
					if (TimeProbablyGotStuck == 0)
					{
						TimeProbablyGotStuck = level.time + 35 * 15;
					}
					else if (level.time > TimeProbablyGotStuck)
					{
						spawn("teleportfog", pos);
						SetOrigin(MyPlayer.pos+(0,0,50), TRUE);
						spawn("teleportfog", pos);
						TimeProbablyGotStuck = 0;
						CDtoTele = false;
					}
					
					// Move towards Player
					A_Recoil(-3);
					A_MovementChase(MyPlayer);
				}
			}
			else
			{
				CDtoTele = false;
				TimeProbablyGotStuck = 0;
				frame = 0;

				// random angle during staying in one place
				if (level.time > TimeBeforeRandomTurn)
				{
					angle = random[ang](-360,360);
					TimeBeforeRandomTurn = level.time + 35 * random[tiem](1,4);
				}
			}
		}
		
		// Look for enemies
		if (!target)
		{
			LookForEnemies(false);
			
			// Acquired a new target
			if (target)
			{
				// Target is too far away
				if (Distance3D(target) > 750)
					target = null; return;
			}
			
		}
		
		// Active target we have
		if (target && !target.bKILLED)
		{
			if (MyPlayer)
			{
				if (Distance3D(MyPlayer) > 425)
				{
					if (!CDtoTele)
					{
						TimeProbablyGotStuck = level.time + 35 * 6;
						CDtoTele = true;
					}
						
					if (CDtoTele && level.time > TimeProbablyGotStuck)
					{
						CDtoTele = false;
						target = null;
						spawn("teleportfog", pos);
						SetOrigin(MyPlayer.pos+(0,0,50), TRUE);
						spawn("teleportfog", pos);
						TimeProbablyGotStuck = 0;
						return;
					}
				}
				else
					CDtoTele = false;
			}
			
			// target is not in line of sight, look for a new target
			// (so they won't mindlessly attack walls)
			/*
			if (!(IsVisible (target, true)))
			{
				target = null;
				Console.Printf("Out of sight");
				return;
			}
			*/
			
			A_MovementChase(target);
			
			A_AllyDodge();
			
			// Closing in or closing out
			
			if (!IsMelee)
			{
				if (target)
				{
					if (Distance3D(target) > 150)
						A_Recoil(-1.75);
					else
						A_Recoil(1.75);
				}
			}
			else
			{
				if (target && Distance3D(target) > 25)
					A_Recoil(-1.75);
			}
			
			
			A_AllyShoot();
		
		}
	}
	
	virtual void A_AllyDodge()
	{
		if (!target) return;
		
		if (!(level.time % 2 == 0)) return;
		
		FLineTraceData lineData;
		double ThrustValue = 3;
		
		// Let's check if we can move right or left, is there a better way to do this?
		if (InStateSequence(target.CurState, target.ResolveState("Missile")) || InStateSequence(target.CurState, target.ResolveState("Melee")))
		{
			bool HitWall1 = false;
			bool HitWall2 = false;
			
			LineTrace(angle+90,150,0, offsetz: 5, data: LineData);
			if (LineData.HitType == TRACE_HitActor || LineData.HitType == TRACE_HitWall)
			{
				//Console.Printf("hit into a wall plus");
				angle = AngleTo(target);
				Thrust(ThrustValue, Angle - 90);
				HitWall1 = true;
			}
			
			LineTrace(angle-90,150,0, offsetz: 5, data: LineData);
			if (LineData.HitType == TRACE_HitActor || LineData.HitType == TRACE_HitWall)
			{
				//Console.Printf("hit into a wall minus");
				angle = AngleTo(target);
				Thrust(ThrustValue, Angle + 90);
				HitWall2 = true;
			}
			
			// Not fool proof, they will still sometimes run into a projectile unfortunately
			/*
			if (!HitWall1 && !HitWall2)
			{
				Thrust(ThrustValue, Angle + 90);
			}
			*/
			
			// Oh no, they are trying to melee attack!
			if (InStateSequence(target.CurState, target.ResolveState("Melee")) && !HitWall1 && !HitWall2)
			{
				if (target.target && target.target == self)
				{
					// Can we even dodge behind?
					LineTrace(-angle,150,0, offsetz: 5, data: LineData);
					if (LineData.HitType == TRACE_HitWall || LineData.HitType == TRACE_HitActor)
					{
						bool rr = random[ly](0,1);
						if (rr)
							Thrust(ThrustValue, Angle - 90);
						else
							Thrust(ThrustValue, Angle + 90);
						
						return;
					}
					
					A_Recoil(1.25);
				}
			}
		}
	}
	
	virtual void HA_RangeShoot()
	{
		if (!IsMelee && CheckLOF(0, 0, 0, 0, 0, 0, 0, AAPTR_TARGET, 0))
		{
			// Implement it in an actor inheriting from HA_CoreAI
		}
	}

	virtual void HA_MeleeShoot()
	{
		if (IsMelee && CheckMeleeRange())
		{
			// Implement it in an actor inheriting from HA_CoreAI
		}
	}

	virtual void A_AllyShoot()
	{
		if (!target) return;
		
		HA_RangeShoot();
		HA_MeleeShoot();
	}
	
	override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		if (source)
		{
			if (source is "PlayerPawn" || source is "HA_CoreAI")
				return Super.DamageMobj(inflictor,source,0,mod,flags,angle);

			attacker = source;
			target = attacker;
			angle = AngleTo(target);
		}
		
		return Super.DamageMobj(inflictor,source,damage,mod,flags,angle);
	}

	// For the chat
	void AllySay(string msg)
	{
		if (HexenColor ~== "Green") 
			Console.Printf("\c[white]%s's \c[green]%s\c*: %s", MyPlayer.player.GetUserName(), HexenType, msg);
		if (HexenColor ~== "Red")
			Console.Printf("\c[white]%s's \c[red]%s\c*: %s", MyPlayer.player.GetUserName(), HexenType, msg);
		if (HexenColor ~== "Blue")
			Console.Printf("\c[white]%s's \c[blue]%s\c*: %s", MyPlayer.player.GetUserName(), HexenType, msg);
		if (HexenColor ~== "Silver")
			Console.Printf("\c[white]%s's \c[white]%s\c*: %s", MyPlayer.player.GetUserName(), HexenType, msg);
		if (HexenColor ~== "Yellow")
			Console.Printf("\c[white]%s's \c[yellow]%s\c*: %s", MyPlayer.player.GetUserName(), HexenType, msg);

		A_StartSound("misc/chat", CHAN_7, attenuation:ATTN_NONE);
	}
	
	// Prints out Obituary (as if they were a Player) to the chat
	void AllyDied()
	{
		string AIname = "Ally Death: Failed to get Ally's Color! (HexenColor variable)";

		if (HexenColor ~== "Green") 
			AIname = String.Format("\c[white]%s's \c[green]%s", MyPlayer.player.GetUserName(), HexenType);
		if (HexenColor ~== "Red")
			AIname = String.Format("\c[white]%s's \c[red]%s", MyPlayer.player.GetUserName(), HexenType);
		if (HexenColor ~== "Blue")
			AIname = String.Format("\c[white]%s's \c[blue]%s", MyPlayer.player.GetUserName(), HexenType);
		if (HexenColor ~== "Silver")
			AIname = String.Format("\c[white]%s's \c[white]%s", MyPlayer.player.GetUserName(), HexenType);
		if (HexenColor ~== "Yellow")
			AIname = String.Format("\c[white]%s's \c[yellow]%s", MyPlayer.player.GetUserName(), HexenType);

		if (attacker)
		{
			string ob = attacker.GetObituary(self, attacker, "none", false);
			ob = StringTable.Localize (ob);
			ob.Replace("%o ", "");

			Console.Printf("%s \c[yellow]%s", AIname, ob);
		}
		// Died to World
		else
			Console.Printf("%s has died.", AIname);
	}
	
	virtual void HA_Tick()
	{
		if (!bKILLED)
			A_AllyDodge();
		
		if (!(pos.z ~== floorz))
			bTHRUSPECIES = true;
		else
			bTHRUSPECIES = false;
		
		/*
		if (bKILLED && MyPlayer && CountdownToDeath != 0 && level.time > CountdownToDeath)
		{
			spawn("TeleportFog",pos);
			Console.Printf("A dead follower has been teleported into it's dimension back...");
			Destroy();
		}
		*/
		
	}
	
	override void Tick()
	{
		Super.Tick();
		
		HA_Tick();
	}

	Default
	{
		Species "HexenAlly";
		Obituary "%o was killed by a Hexen Ally. This wasn't supposed to happen!";
		Speed 5;
		Health 100;
		Radius 16;
		Height 64;
		Mass 100;
		PainChance 255;
		MaxStepHeight 50;
		MONSTER;
		+FRIENDLY;
		+BLOCKASPLAYER;
		+PUSHABLE;
		+ACTIVATEIMPACT;
		+CANPUSHWALLS;
		+CANUSEWALLS;
		-ACTIVATEMCROSS;
		+NOTELESTOMP;
		+NOBLOCKMONST;
		+NOTIMEFREEZE;
		+NOFEAR;
		+SEEINVISIBLE;
		+DONTHARMCLASS;
		+DONTHURTSPECIES;
		+NOTELEFRAG;
		+JUMPDOWN;
		+NOFEAR;
		+DROPOFF
		//+THRUSPECIES
		+MTHRUSPECIES
		-COUNTKILL;
		-ISMONSTER;
	}
}
