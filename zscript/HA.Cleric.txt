// Cleric AI
class HA_Cleric : HA_CoreAI
{
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		ActiveWeapon = "Mace";
		IsMelee = true;
	}

	override void HA_MeleeShoot()
	{
		if (!IsMelee) return;
		if (!CheckMeleeRange()) return;

		if (ActiveWeapon ~== "Mace")
		{
			FTranslatedLineTarget t;

			int damage = random[MaceAtk](25, 40);
			for (int i = 0; i < 16; i++)
			{
				for (int j = 1; j >= -1; j -= 2)
				{
					double ang = angle + j*i*(45. / 16);
					double slope = AimLineAttack(ang, 2 * DEFMELEERANGE, t, 0., ALF_CHECK3D);
					if (t.linetarget)
					{
						LineAttack(ang, 2 * DEFMELEERANGE, slope, damage, 'Melee', "HammerPuff", true, t);
						if (t.linetarget != null)
						{
							AdjustPlayerAngle(t);
							SetStateLabel("Melee");
							vel.xy = (0,0);
							return;
						}
					}
				}
			}
			// didn't find any creatures, so try to strike any walls

			double slope = AimLineAttack (angle, DEFMELEERANGE, null, 0., ALF_CHECK3D);
			LineAttack (angle, DEFMELEERANGE, slope, damage, 'Melee', "HammerPuff");
			SetStateLabel("Melee");
			vel.xy = (0,0);
		}
	}

	Default
	{
		Species "HexenAlly";
		PainSound "PlayerClericPain";
		DeathSound "PlayerClericNormalDeath";
	}
	States
	{
	Spawn:
		CLER AAA 4;
		Loop;
	See:
		CLER A 0 A_AlertMonsters(184, AMF_TARGETEMITTER);
		CLER ABCD 4 A_FollowerChase();
		Loop;
	Missile:
		Goto Melee;
	Melee:
		CLER E 0 A_AlertMonsters(512, AMF_TARGETEMITTER);
		CLER EF 4 A_AllyDodge();
		CLER G 17 A_AllyDodge(); //was 8
		CLER FE 2 A_AllyDodge();
		Goto See;
	Pain:
		CLER H 4;
		CLER H 4 A_Pain;
		Goto See;
	Death:
		TNT1 A 0
		{
			CountdownToDeath = level.time + 35 * 5;
			AllyDied();
		}
		CLER I 6;
		CLER J 6 A_Scream;
		CLER KL 6;
		CLER M 6 A_NoBlocking;
		CLER NOP 6;
		CLER Q -1;
		Stop;		
	XDeath:
		TNT1 A 0
		{
			CountdownToDeath = level.time + 35 * 5;
			AllyDied();
		}
		CLER R 5 A_Scream;
		CLER S 5;
		CLER T 5 A_NoBlocking;
		CLER UVWXYZ 5;
		CLER [ -1;
		Stop;
	Ice:
		TNT1 A 0 AllyDied();
		CLER \ 5 A_FreezeDeath;
		CLER \ 1 A_FreezeDeathChunks;
		Wait;
	Burn:
		TNT1 A 0 AllyDied();
		FDTH A 5 BRIGHT A_StartSound("*burndeath");
		FDTH B 4 BRIGHT;
		FDTH G 5 BRIGHT;
		FDTH H 4 BRIGHT A_Scream;
		FDTH I 5 BRIGHT;
		FDTH J 4 BRIGHT;
		FDTH K 5 BRIGHT;
		FDTH L 4 BRIGHT;
		FDTH M 5 BRIGHT;
		FDTH N 4 BRIGHT;
		FDTH O 5 BRIGHT;
		FDTH P 4 BRIGHT;
		FDTH Q 5 BRIGHT;
		FDTH R 4 BRIGHT;
		FDTH S 5 BRIGHT A_NoBlocking;
		FDTH T 4 BRIGHT;
		FDTH U 5 BRIGHT;
		FDTH V 4 BRIGHT;
		ACLO E 35 A_CheckPlayerDone;
		Wait;
		ACLO E 8;
		Stop;	
	
	}
}
