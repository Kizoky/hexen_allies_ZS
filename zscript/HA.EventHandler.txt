class HA_Blessed : Inventory 
{
	Actor LastAttacker;
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (owner && source)
		{
			LastAttacker = source;
		}
	}
}

// not directly inherited from HA_Blessed intentionally
class HA_Uni : Inventory
{
	Actor LastAttacker;
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (owner && source)
		{
			LastAttacker = source;
		}
	}
}

class HexenAlliesCoreHandler : EventHandler 
{
	Array<Actor> Allies;
	Array<Actor> Bosses;
	bool BossSpawn;
	
	void BossSay(string msg, actor Boss)
	{
		string BossName = Boss.GetTag();

		Console.Printf("%s\c*: %s", BossName, msg);
		Boss.A_StartSound("misc/chat", CHAN_7, attenuation:ATTN_NONE);
	}

	override void WorldTick()
	{
		Super.WorldTick();
		if (level.time > 35 * 7 && BossSpawn)
		{
			// Based on current Total Allies
			int TotalAllies = 0;
			for (uint i = 0; i < MAXPLAYERS; i++)
			{
				if (Players[i].mo)
				{
					let HubInv = HA_AIHub(Players[i].mo.FindInventory("HA_AIHub"));
					if (HubInv)
					{
						TotalAllies += HubInv.TotalNum;
					}
				}
			}

			for (uint i = 0; i < Bosses.size(); i++)
			{
				if (Bosses[i] && !Bosses[i].bKILLED && !Bosses[i].FindInventory("HA_Blessed"))
				{
					Bosses[i].Health += TotalAllies * 100;
					Bosses[i].PainChance -= TotalAllies * 4;
					Bosses[i].A_GiveInventory("HA_Blessed", 1);
					BossSay("The Game Master has blessed me powers!", Bosses[i]);
				}
			}

			BossSpawn = false;
		}
	}
			
	override void NetworkProcess(ConsoleEvent e)
    {
		if (e.Name.IndexOf("InfoButton") >= 0)
		{
            Array<String> infoCmd;
            e.Name.Split(infoCmd, ":");
            if (infoCmd[1] ~== "Guard" || infoCmd[1] ~== "Follow")
            {
				int allyNum = infoCmd[2].ToInt();

				PlayerPawn Player = PlayerPawn(players[e.Player].mo);
				if (Player)
				{
					let HubInv = HA_AIHub(Player.FindInventory("HA_AIHub"));
					if (HubInv)
					{
						if (infoCmd[1] ~== "Guard")
							HubInv.GetAllyByIndex(allyNum).ForceGuard();
						else if (infoCmd[1] ~== "Follow")
							HubInv.GetAllyByIndex(allyNum).ForceFollow();
					}
				}
            }
		}

		// ----------------------------------------------------------------------------------------------
		// ----------------------------------------------------------------------------------------------
		if (e.Name ~== "AllyMenu_Spawn")
		{
			PlayerPawn Player = PlayerPawn(players[e.Player].mo);
			if (Player)
			{
				let AllySpawner = HA_AllySpawner(Player.FindInventory("HA_AllySpawner"));
				if (AllySpawner)
				{
					AllySpawner.SpawnAlly();
				}
			}
		}

		if (e.Name ~== "AllyMenu_Increment")
		{
			PlayerPawn Player = PlayerPawn(players[e.Player].mo);
			if (Player)
			{
				let AllyIncrement = HA_AllySlot(Player.FindInventory("HA_AllySlot"));
				if (AllyIncrement)
				{
					AllyIncrement.TryIncrementing();
				}
			}
		}

		if (e.Name ~== "AllyMenu_IncrementMaxSlot")
		{
			PlayerPawn Player = PlayerPawn(players[e.Player].mo);
			if (Player)
			{
				let AllyIncrement = HA_AllySlot(Player.FindInventory("HA_AllySlot"));
				if (AllyIncrement)
				{
					AllyIncrement.IncreaseMaxSlot();
				}
			}
		}
		// ----------------------------------------------------------------------------------------------
		// ----------------------------------------------------------------------------------------------
	}

	override void PlayerEntered (PlayerEvent e)
	{
		PlayerPawn n = PlayerPawn(players[e.PlayerNumber].mo);
		if (n)
		{
			// Make sure to only give Core items once per game session
			if (n.CountInv("HA_AIHub") <= 0)
			{
				n.A_GiveInventory("HA_AIHub", 1);
				
				// Usable Inventory items
				n.A_GiveInventory("HA_AllySpawner", 1);
				n.A_GiveInventory("HA_AllySlot", 1);
			}
		}
	}

	override void WorldThingSpawned (Worldevent e)
	{
		// Store every Ally on the level to remove them later when the World is unloaded
		let SpawnedThing = e.Thing;
		if (SpawnedThing)
		{
			if (SpawnedThing is "HA_CoreAI")
			{
				Allies.Push(SpawnedThing);
			}
			
			// Challenge the Player by reducing the Bosses' Pain Chance and increasing Health
			if (SpawnedThing.bBOSS || IsHexenBoss(SpawnedThing))
			{
				if (!BossSpawn) 
					BossSpawn = true;
				
				Bosses.Push(SpawnedThing);
			}
			else if (SpawnedThing.bISMONSTER)
			{
				SpawnedThing.A_GiveInventory("HA_Uni", 1);
			}
		}
	}
	
	override void WorldUnloaded (WorldEvent e)
	{
		// Update information of Allies
		// Health, Active Weapon, etc...
		for (uint i = 0; i < MAXPLAYERS; i++)
		{
			if (Players[i].mo)
			{
				let HubInv = HA_AIHub(Players[i].mo.FindInventory("HA_AIHub"));
				if (HubInv)
				{
					HubInv.UpdateAlly();
				}
			}
		}

		// Destroy all Allies, if we don't, Players will have duplicate amount of allies
		// Of course the dead ones should lay on the floor
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i] && !Allies[i].bKILLED)
			{
				Allies[i].Destroy();
			}
		}
	}

	override void WorldLoaded (WorldEvent e)
	{
		// This is where the allies will get "respawned" for the Players
		for (uint i = 0; i < MAXPLAYERS; i++)
		{
			if (Players[i].mo)
			{
				let HubInv = HA_AIHub(Players[i].mo.FindInventory("HA_AIHub"));
				if (HubInv)
				{
					HubInv.RestoreAlly();
				}
			}
		}
	}

	static const string HexenBossList[] =
	{
		"korax",
		"heresiarch",
		"death wywern",
		"boss",
		"traductus",
		"menelkir",
		"zedek"
	};

	bool IsHexenBoss(actor Whom)
	{
		bool IsBoss = false;
		if (Whom && Whom.bISMONSTER)
		{
			string BossName = Whom.GetTag();
			BossName = BossName.MakeLower();
			
			for (uint i = 0; i < HexenBossList.Size(); i++)
			{
				if (BossName.IndexOf(HexenBossList[i]) >= 0)
				{
					IsBoss = true;
					break;
				}
			}
		}
		
		return IsBoss;
	}

	override void WorldThingDied(WorldEvent e)
	{	
		if (e.Thing)
		{
			// Remove dead Ally from the database, never attempt to spawn dead allies
			// Also make sure we remove it from the correct Player
			if (e.Thing is 'HA_CoreAI')
			{
				let Ally = HA_CoreAI(e.Thing);
				if (Ally)
				{
					if (Ally.MyPlayer)
					{
						Ally.AllyObituary();
						
						let HubInv = HA_AIHub(Ally.MyPlayer.FindInventory("HA_AIHub"));
						if (HubInv)
						{
							HubInv.RemoveAlly(Ally);
						}
					}
				}
			}

			if (e.Thing.bBOSS || IsHexenBoss(e.Thing))
			{
				let Blessed = HA_Blessed(e.Thing.FindInventory("HA_Blessed"));
				if (Blessed)
				{
					if (Blessed.LastAttacker is 'HA_CoreAI')
					{
						let Ally = HA_CoreAI(Blessed.LastAttacker);
						if (Ally)
						{
							string BossName = e.Thing.GetTag();
							string AllyName = Ally.AllyNameColorizedPlus();
							
							Console.Printf("%s \c[yellow]has been slain by \c[white]%s", BossName, AllyName);
						}
					}
				}
			}
			
			// Make sure we have a player in-game..
			/*
			actor Player = null;
			for (uint i = 0; i < MAXPLAYERS; i++)
			{
				if (Players[i].mo)
				{
					Player = Players[i].mo;
					break;
				}
			}
			*/

			if (!(e.Thing is "PlayerPawn") && 
				!e.Thing.bFRIENDLY && 
				!(e.Thing is "HA_CoreAI") && 
				e.Thing.bIsMonster)
			{
				if (!IsHexenBoss(e.Thing))
				{
					int x = random[xsoul](-30,30);
					int y = random[ysoul](-30,30);
					e.Thing.spawn("HA_Soul", e.thing.pos+(x,y,35));

					let pInv = HA_Uni(e.Thing.FindInventory("HA_Uni"));
					if (pInv)
					{
						let pAttacker = HA_CoreAI(pInv.LastAttacker);
						if (pAttacker)
						{
							pAttacker.OverallKills++;
						}
					}
				}
				else
				{
					// Bosses should spawn more Souls
					int MaxSouls = random[bssls](7,14);
					for (uint i = 0; i < MaxSouls; i++)
					{
						int x = random[xboss](-25,25);
						int y = random[yboss](-25,25);
						int z = random[zboss](1,45);
						e.Thing.spawn("HA_Soul", e.thing.pos+(x,y,z));
					}

					let pInv = HA_Blessed(e.Thing.FindInventory("HA_Blessed"));
					if (pInv)
					{
						let pAttacker = HA_CoreAI(pInv.LastAttacker);
						if (pAttacker)
						{
							pAttacker.OverallKills++;
						}
					}
				}
			}
		}
		
	}
}