class HexenAlliesCoreHandler : EventHandler 
{
	Array<Actor> Allies;

	/*
	override void WorldTick()
	{
		//let player = players[consoleplayer].mo;
	}

	override void NetworkProcess (ConsoleEvent e)
	{
		//
	}
   */

	override void PlayerEntered (PlayerEvent e)
	{
		PlayerPawn n = PlayerPawn(players[e.PlayerNumber].mo);
		if (n)
		{
			n.A_GiveInventory("HA_AIHub", 1);
			
			n.A_GiveInventory("HA_AllySpawner", 1);
			n.A_GiveInventory("HA_AllySlot", 2);
		}
	}

	override void WorldThingSpawned (Worldevent e)
	{
		// Store every Ally on the level to remove them later when the World is unloaded
		let SpawnedThing = e.Thing;
		if (SpawnedThing && SpawnedThing is "HA_CoreAI")
		{
			Allies.Push(SpawnedThing);
		}
	}
	
	override void WorldUnloaded (WorldEvent e)
	{
		// Update information of Allies
		// Health, Active Weapon, etc...
		for (uint i = 0; i < MAXPLAYERS; i++)
		{
			if (Players[i].mo)
			{
				let HubStuff = Players[i].mo.FindInventory("HA_AIHub");
				if (HubStuff)
				{
					let Inven = HA_AIHub(HubStuff);
					if (Inven)
					{
						Inven.UpdateAlly();
					}
				}
			}
		}

		// Destroy all Allies, if we don't, Players will have duplicate amount of allies
		// Of course the dead ones should lay on the floor
		for (uint i = 0; i < Allies.size(); i++)
		{
			if (Allies[i] && !Allies[i].bKILLED)
			{
				Allies[i].Destroy();
			}
		}
	}

	override void WorldLoaded (WorldEvent e)
	{
		// This is where the allies will get "respawned" for the Players
		for (uint i = 0; i < MAXPLAYERS; i++)
		{
			if (Players[i].mo)
			{
				let HubStuff = Players[i].mo.FindInventory("HA_AIHub");
				if (HubStuff)
				{
					let Inven = HA_AIHub(HubStuff);
					if (Inven)
					{
						Inven.RestoreAlly();
					}
				}
			}
		}
	}

	override void WorldThingDied(WorldEvent e)
	{	
		if (e.Thing)
		{
			// Remove dead Ally from the database, never attempt to spawn dead allies
			// Also make sure we remove it from the correct Player
			if (e.Thing is 'HA_CoreAI')
			{
				let Ally = HA_CoreAI(e.Thing);
				if (Ally)
				{
					if (Ally.MyPlayer)
					{
						Ally.AllyObituary();
						
						let HubStuff = Ally.MyPlayer.FindInventory("HA_AIHub");
						if (HubStuff)
						{
							let Inven = HA_AIHub(HubStuff);
							if (Inven)
							{
								Inven.RemoveAlly(Ally);
							}
						}
					}
				}
			}

			// Make sure we have a player in-game..
			actor Player = null;
			for (uint i = 0; i < MAXPLAYERS; i++)
			{
				if (Players[i].mo)
				{
					Player = Players[i].mo;
					break;
				}
			}

			if (!(e.Thing is "PlayerPawn") && !e.Thing.bFRIENDLY && !(e.Thing is "HA_CoreAI") && e.Thing.bIsMonster)
			{
				if (Player)
					Player.spawn("HA_Soul", e.thing.pos+(0,0,35));
			}
		}
		
	}
}